// F3 - Briefing - Credits: Please see the F3 online manual http://www.ferstaberinde.com/f3/en/
// <marker name='XXX'>XXX</marker>
// Green #80FF00
// Orange #FF7F00
// Blue #00FFFF

if (isNil "f_param_CasualtiesCap") then { f_param_CasualtiesCap = 100 }; // Set CasCap if author did not.

// The code below creates the administration sub-section of notes.
_adm = player createDiaryRecord ["Diary", ["Administration",[] call f_fnc_fillAdministration]];

// Edit the if statement for different faction briefs.

if (side group player != CIVILIAN) then {
	// The code below creates the execution sub-section of notes.
	_exe = player createDiaryRecord ["Diary", ["Mission",format["
	<br/><font size='18' color='#80FF00'>OBJECTIVES</font>
	<br/>Our intelligence reports indicate that German forces have established a strategic presence in the Beaucoudray area and they are preparing to deploy an Anti-Tank (AT) gun at Le Hamel Au Breton. This AT gun poses a significant threat to our advancing forces. Additionally, we have identified a bunker at the Beaucoudray intersection that is vital for the enemy's communication network. Securing this bunker will disrupt their command and control capabilities.
	<br/>
	<br/><font color='#FF7F00'>AT Gun Destruction</font>
	<br/>Proceed to Le Hamel Au Breton to neutralize the AT gun.
	<br/>Timing is critical. You must prevent the AT gun from being deployed.
	<br/>
	<br/><font color='#FF7F00'>Bunker Capture</font>
	<br/>Eliminate resistance and secure the bunker. It is crucial for disrupting enemy communications.
	<br/>Expect light enemy presence initially, but be prepared for reinforcements.
	<br/>
	<br/><font color='#FF7F00'>Extraction</font>
	<br/>Once objectives are complete, head to the extraction point at LZ Bravo.
	<br/>Expect enemy counterattacks during the extraction phase.
	<br/>
	<br/>%1 <!--- Casualties Cap --->
	<br/><font size='18' color='#80FF00'>CREDITS</font>
	<br/>Created by <font color='#FF0080'>2600K</font color>
	<br/>
	<br/>A custom-made mission for ArmA 3 and Zeus Community
	<br/>http://zeus-community.net
	<br/>",
	if (f_param_CasualtiesCap > 0 && f_param_CasualtiesCap < 100) then { format["Ensure casualties are kept below %1 and %1&#37; of your force is not incapacitated.<br/>", f_param_CasualtiesCap] } else { "" },
	(((vehicles select { side _x getFriend side group player < 0.6 && !(_x isKindOf "staticWeapon" || _x isKindOf "static") && count crew _x > 0}) apply {  getText (configFile >> "CfgVehicles" >> typeOf _x >> "displayName") }) call BIS_fnc_consolidateArray) apply { format["%2x <font color='#00FFFF'>%1</font><br/>", _x#0, _x#1] } joinString "",
	format["%1x <font color='#00FFFF'>Infantry Groups</font><br/>", { side _x getFriend side group player < 0.6 && count units _x >= 3 && vehicle leader _x == leader _x} count allGroups],
	((allUnits select { side _x getFriend side group player < 0.6 && count crew _x > 0 && (vehicle _x != _x || secondaryWeapon _x != "") && (getarray(configFile >> "CfgVehicles" >> typeOf vehicle _x >> "threat")#1 >= 0.9 || getarray(configFile >> "CfgVehicles" >> typeOf vehicle _x >> "threat")#2 == 1) } apply {  getText (configFile >> "CfgVehicles" >> typeOf (if (secondaryWeapon _x isEqualTo "") then { vehicle _x } else { _x }) >> "displayName") }) call BIS_fnc_consolidateArray) apply { format["%2x <font color='#00FFFF'>%1</font><br/>", _x#0, _x#1] } joinString ""
	]]];
};

// Commander Endings
if (rank player in ["MAJOR","COLONEL"]) then {
	private _missionEndings = "<font size='18' color='#80FF00'>ENDINGS</font><br/><br/>These endings are available. To trigger an ending click on its link - It will take a few seconds to synchronise across all clients.<br/><br/>";

	for "_i" from 1 to 15 do {
		private _eID = format ["end%1",_i];

		if (getText (getMissionConfig "CfgDebriefing" >> _eID >> "Title") != "") then {	
			_missionEndings = _missionEndings + format [
				"%4<br/><execute expression=""['f_briefing.sqf','End%1 called by %5','INFO'] remoteExec ['f_fnc_logIssue',2]; 'End%1' remoteExec ['BIS_fnc_endMission'];"">Ending #%1</execute> - %2:<br/>%3<br/><br/>",
				_i,
				getText (getMissionConfig "CfgDebriefing" >> _eID >> "title"),
				getText (getMissionConfig "CfgDebriefing" >> _eID >> "description"),
				format["<img image='%1' height='32'/>", getText (getMissionConfig "CfgDebriefing" >> _eID >> "picture")],
				name player
			];	
		};
	};

	// Create the briefing section to display the endings
	player createDiaryRecord ["Diary", ["Endings",_missionEndings]];
};